// Fox and Hounds (Wolf and Sheep)
// -- The player that is unable to move loses.
// -- Fox wins when reaches the hounds' back rank.
// -- In the first turn, fox selects any initial square of the four in his back rank.

#players = foxPlayer(100), houndPlayer(100)
#pieces = e, f, h
#variables =
#board = rectangle(upRight,downLeft,upLeft,downRight,
         [ , , ,e, , , ]
         [ , ,e,e,e, , ]
         [ ,e,e,e,e,e, ]
         [e,e,e,e,e,e,e]
         [e,e,e,e,e,e,h]
         [ ,e,e,e,e,h, ]
         [ , ,e,e,h, , ]
         [ , , ,h, , , ])

#anySquare = ((upRight + downLeft + upLeft + downRight)*)

#foxTurn(forward) = (
    ->foxPlayer anySquare {f} [e] (upRight + downLeft + upLeft + downRight) {e} [f]
    ->> [$ foxPlayer = 100, houndPlayer = 0]
    (   {? forward~Left + forward~Right}
      + {! forward~Left + forward~Right} ->> {})
  )

#houndTurn(forward) = (
    ->houndPlayer anySquare {h} ->houndPlayer [e] (forward~Left + forward~Right) {e} [h]
    ->> [$ foxPlayer = 0, houndPlayer = 100]
  )

#rules = ->foxPlayer anySquare {! upLeft + upRight} [f]
    (foxTurn(down) houndTurn(up))*
