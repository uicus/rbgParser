// Fox and Hounds (Wolf and Sheep)
// -- The player that is unable to move loses.
// -- Fox wins when reaches the hounds' back rank.
// -- In the first turn, fox selects any initial square of the four in his back rank.

#players = foxPlayer(100), houndPlayer(100)
#pieces = e, w, s
#variables =
#board = rectangle(upRight,downLeft,upLeft,downRight,
         [ , , ,e, , , ]
         [ , ,e,e,e, , ]
         [ ,e,e,e,e,e, ]
         [e,e,e,e,e,e,e]
         [e,e,e,e,e,e,s]
         [ ,e,e,e,e,s, ]
         [ , ,e,e,s, , ]
         [ , , ,s, , , ])

#anySquare = ((upRight + downLeft + upLeft + downRight)*)

#foxTurn(forward) = (
    ->foxPlayer anySquare {w} [e] (upRight + downLeft + upLeft + downRight) {e} [w]
    ->> [$ foxPlayer = 100, houndPlayer = 0] 
    (   {? forward~Left + forward~Right}
      + {! forward~Left + forward~Right} ->> {})
  )

#houndTurn(forward) = (
    ->houndPlayer anySquare {s} [e] (forward~Left + forward~Right) {e} [s]
    ->> [$ foxPlayer = 0, houndPlayer = 100]
  )
     
#rules = ->foxPlayer anySquare {! upLeft + upRight} [w]
    (foxTurn(down) houndTurn(up))*
