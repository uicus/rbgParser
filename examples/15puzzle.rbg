// 15-puzzle
// -- Fixed initial configuration
// -- 100 turn limit

#players = player(100)
#pieces = e,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O
#variables = counter(100)

#board = rectangle(up, down, left, right,
    [e, O, N, M]
    [L, K, J, I]
    [H, G, F, E]
    [D, C, A, B])

#anyNeighbour = (left + right + up + down)

#anySquare = ((up* + down*)(left* + right*))

#findEmpty = (anySquare {e})

#swap(square) = {square} [e] anyNeighbour {e} ->> [square]

#swapNeighbour = (findEmpty anyNeighbour (
      swap(A) + swap(B) + swap(C) + swap(D) + swap(E) +
      swap(F) + swap(G) + swap(H) + swap(I) + swap(J) +
      swap(K) + swap(L) + swap(M) + swap(N) + swap(O)
    )
  )

#isSorted = (anySquare {! right + down}
    {e} left  {O} left  {N} left  {M} up 
    {I} right {J} right {K} right {L} up
    {H}  left {G} left  {F} left  {E} up 
    {A} right {B} right {C} right {D}
)

#turn = (
    [$ counter = counter+1] (
        {! isSorted} ->player swapNeighbour
      + {? isSorted} [$ player = 100] ->> {}
    )
  )

#rules = turn*
